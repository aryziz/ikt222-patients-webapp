plugins {
  id 'java'
  id 'application'
  id 'idea'
}

repositories {
  mavenCentral()
}

dependencies {
  implementation (
    'org.eclipse.jetty.aggregate:jetty-all:9.4.24.v20191120',
    'org.freemarker:freemarker:2.3.29',
    'org.xerial:sqlite-jdbc:3.50.3.0',
    'io.github.cdimascio:dotenv-java:3.0.0',
    'org.mindrot:jbcrypt:0.4'
  )

  runtimeOnly 'org.slf4j:slf4j-simple:2.0.13'
}

application {
  // Allow overriding the main class via -PmainClass when running with Gradle.
  // Default to the AppServer so normal runs behave the same.
  mainClass = project.hasProperty('mainClass') ? project.getProperty('mainClass') : 'IKT222.Assignment4.AppServer'
}

idea {
  module {
    outputDir file('build/classes/main')
    testOutputDir file('build/classes/test')
  }
}

// Convenience task to run the one-off DB migration utility without messing with
// Gradle project properties. Use `./gradlew runHashPasswords` to execute.
task runHashPasswords(type: JavaExec) {
  group = 'Migration'
  description = 'Run HashPasswords to migrate plain-text passwords to BCrypt in the DB'
  classpath = sourceSets.main.runtimeClasspath
  main = 'IKT222.Assignment4.HashPasswords'
}

task copyDependencies(type: Copy) {
    from configurations.runtimeClasspath
    into 'lib'
}

// Task to run the password hashing test
task testPasswordHashing(type: JavaExec) {
    group = 'Verification'
    description = 'Run TestPasswordHashing to verify BCrypt implementation'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'IKT222.Assignment4.TestPasswordHashing'
}

task submission(type: Zip) {
    group = 'Submission'
    description = 'Generates Zip archive suitable for submission.'

    archiveFileName = 'submission.zip'
    destinationDirectory = project.rootDir

    from("$project.rootDir") {
        exclude 'submission.zip', 'build', '.git', '.gradle', '.idea'
    }

    doFirst {
        assert file("report.pdf").exists()
    }

    doLast {
        logger.warn("submission.zip generated.")
    }
}
